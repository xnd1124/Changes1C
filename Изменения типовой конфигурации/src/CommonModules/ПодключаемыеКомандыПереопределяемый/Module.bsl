
&После("ПриОпределенииВидовПодключаемыхКоманд")
Процедура Изм_ПриОпределенииВидовПодключаемыхКоманд(ВидыПодключаемыхКоманд)
	 Вид = ВидыПодключаемыхКоманд.Добавить();
	 Вид.Имя         = "ИзмененияВКонфигурации";
	 Вид.ИмяПодменю  = "ИзмененияВКонфигурации";
	 Вид.Заголовок   = НСтр("ru = 'Изменения в конфигурации'");
	 Вид.Картинка    = БиблиотекаКартинок.Изм_Git;
	 Вид.Отображение = ОтображениеКнопки.КартинкаИТекст; 

КонецПроцедуры

&После("ПриОпределенииКомандПодключенныхКОбъекту")
Процедура Изм_ПриОпределенииКомандПодключенныхКОбъекту(НастройкиФормы, Источники, ПодключенныеОтчетыИОбработки, Команды)
	МетаданныеКоманды = Неопределено;
	Для каждого Строка Из Источники.Строки Цикл
		МетаданныеКоманды = Строка.Метаданные;
		Прервать;
	КонецЦикла;
	Если МетаданныеКоманды = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	ИдентификаторМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеКоманды, Ложь); 
	Если Не ЗначениеЗаполнено(ИдентификаторМетаданных) Тогда
		Возврат;
	КонецЕсли;
	ЭтоИзмененныеМетаданные = Изм_ПодключаемыеКомандыВызовСервера.ЭтоИзмененныеМетаданные(ИдентификаторМетаданных);   
	
	Если ЭтоИзмененныеМетаданные Тогда 
		ТекстКоманды = "Изменения в конфигурации"; 
	Иначе
		ТекстКоманды = "Изменения не найдены"; 	
	КонецЕсли;	
	
	ДобавитьКоманду(Команды,
	"ИзмененияВКонфигурации",
	ТекстКоманды,
	"Изм_ПодключаемыеКомандыКлиент.ОткрытьСписокТехПроектовСОтборомПоМетаданным",
	,
	Истина);	
	
КонецПроцедуры


Процедура ДобавитьКоманду(Команды, ИдентификаторПодменю, Представление, Обработчик, Порядок = 75, 
							ТолькоВоВсехДействиях = Ложь, ДополнительныеСвойства = Неопределено)
	
	НоваяКоманда = Команды.Добавить();
	НоваяКоманда.Порядок = Порядок;
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Вид = ИдентификаторПодменю;
	НоваяКоманда.МножественныйВыбор = Ложь;
	НоваяКоманда.РежимЗаписи = "НеЗаписывать";
	НоваяКоманда.ИзменяетВыбранныеОбъекты = Ложь;
	НоваяКоманда.Обработчик = Обработчик;
	НоваяКоманда.ТолькоВоВсехДействиях = ТолькоВоВсехДействиях;
	
	Если ДополнительныеСвойства <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(НоваяКоманда, ДополнительныеСвойства);
	КонецЕсли;
	
КонецПроцедуры
