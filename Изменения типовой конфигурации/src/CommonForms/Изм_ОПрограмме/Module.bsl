#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СведенияОРасширении = СведенияОРасширении();
	Версия = СведенияОРасширении.Версия;
	ЧтоНового = СведенияОРасширении.АдресИнформацииОКонфигурации;
	Авторы = ФорматированныеАвторы(СведенияОРасширении.ПодробнаяИнформация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ЧтоНовогоНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПерейтиПоНавигационнойСсылке(ЧтоНового);
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция СведенияОРасширении() 
	УстановитьПривилегированныйРежим(Истина);
	Отбор = Новый Структура("Имя", "ИзмененияТиповойКонфигурации");
	НайденныеРасширения = РасширенияКонфигурации.Получить(Отбор);
	
	Результат = Новый Структура;  
	ТекущееРасширение = НайденныеРасширения[0];
	Результат.Вставить("Версия", НайденныеРасширения[0].Версия); 
	МетаданныеРасширения = Новый ОбъектМетаданныхКонфигурация(ТекущееРасширение.ПолучитьДанные());
	Результат.Вставить("АдресИнформацииОКонфигурации", МетаданныеРасширения.АдресИнформацииОКонфигурации);
	Результат.Вставить("ПодробнаяИнформация", МетаданныеРасширения.ПодробнаяИнформация);
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ФорматированныеАвторы(ПодробнаяИнформация)
	Части = СтрРазделить(ПодробнаяИнформация, Символы.ПС);
	МассивСтрок = Новый Массив;
	Отступ = Символы.Таб + Символы.Таб;
	Для Каждого Часть Из Части Цикл 
		Если СтрНайти(Часть, "https") = 0  тогда
			Строка = Новый ФорматированнаяСтрока(Отступ + Часть);
			МассивСтрок.Добавить(Строка);
		иначе
			Строка = Новый ФорматированнаяСтрока(Часть, , , , Часть);
			МассивСтрок.Добавить(Строка);
			МассивСтрок.Добавить(Символы.ПС);
		КонецЕсли;	
	КонецЦикла;
	Результат = Новый ФорматированнаяСтрока(МассивСтрок);
	Возврат Результат;
КонецФункции
#КонецОбласти
